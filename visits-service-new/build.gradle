plugins {
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id 'io.freefair.lombok' version '6.6.1'
    id 'jacoco'
}

group = 'com.petclinic.visits-service-new'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '17'


repositories {
    mavenCentral()
}
ext {
    mapstructVersion = "1.5.5.Final"
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive',
            'org.springframework.boot:spring-boot-starter-webflux',
            'org.yaml:snakeyaml:2.2',
            "org.mapstruct:mapstruct:${mapstructVersion}",
            'org.simplejavamail:simple-java-mail:8.3.1', 'net.markenwerk:utils-mail-dkim:2.0.1', //Simple Java Mail and DKIM a framework for DKIM signing and verification
            'com.squareup.retrofit2:retrofit:2.9.0', 'com.squareup.retrofit2:converter-jackson:2.9.0'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    compileOnly 'org.projectlombok:lombok',
            "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok',
            "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0', 'com.squareup.okhttp3:okhttp:4.11.0',
            'org.simplejavamail:simple-java-mail:8.3.1', 'net.markenwerk:utils-mail-dkim:2.0.1', //Simple Java Mail and DKIM a framework for DKIM signing and verification
            'org.springframework.boot:spring-boot-starter-test',
            'org.yaml:snakeyaml:2.2',
            'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring30x:4.9.2',
            'io.projectreactor:reactor-test',
            'com.squareup.retrofit2:retrofit-mock:2.9.0'
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/petclinic/visits/visitsservicenew/VisitsServiceNewApplication.class"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification


test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
